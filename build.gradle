buildscript {
    ext {
        dependencyVersion = [
                kotlin: '1.2.21',
                junit : '5.0.2',
                hamcrest: '1.3',
                guava: '24.0-jre'
        ]
        pluginVersion = [
                junit : '1.0.2',
                kotlin: dependencyVersion.kotlin,
                dokka: '0.9.16-eap-1'
        ]
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { // TODO: Remove this repo once upgraded off eap to 0.9.16
            url 'https://dl.bintray.com/kotlin/kotlin-eap/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${pluginVersion.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${pluginVersion.dokka}"
        classpath "org.junit.platform:junit-platform-gradle-plugin:${pluginVersion.junit}"
    }
}

allprojects {
    version = '0.0.1-SNAPSHOT'
    group = 'io.tarro'

    ext {
        moduleName = "${group}.${project.name}"
    }
}

def getGitHash = { ->
    def sink = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = sink
    }
    return sink.toString().trim()
}

def mostRecentFileModifyTime = { -> // FIXME: Currently unused
    return Files.walk(Paths.get("${projectDir}"))
            .map { it.toFile().lastModified() }
            .sorted(Comparator.comparingLong { it }.reversed())
            .findFirst()
            .map { Instant.ofEpochMilli(it).toString() }
            .orElse("<UNKNOWN>")
}

def javaVersion = { ->
    return 'Java ' + System.getProperty('java.version') + ' (' +
            System.getProperty('java.vendor') + ')'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    dependencies {
        testCompile "org.junit.jupiter:junit-jupiter-api:${dependencyVersion.junit}"
        testCompile "org.junit.jupiter:junit-jupiter-params:${dependencyVersion.junit}"
        testCompile "org.hamcrest:hamcrest-all:${dependencyVersion.hamcrest}"
        testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${dependencyVersion.kotlin}"
        testCompile "com.google.guava:guava:${dependencyVersion.guava}"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:${dependencyVersion.junit}"
    }

    checkstyle  {
        configDir = rootProject.file('config/checkstyle/')
        configFile = new File(configDir, 'checkstyle.xml')
        configProperties = [
            configDir: configDir
        ]
        showViolations false
        toolVersion = 8.7
    }

    compileJava {
        // Needed to make the Gradle compile phase work with Java 9/Jigsaw.
        // https://guides.gradle.org/building-java-9-modules
        inputs.property("moduleName", moduleName)
        options.compilerArgs << '-Xlint:all' << '-Werror'
        doFirst {
            options.compilerArgs << '--module-path' << classpath.asPath
            classpath = files()
        }
    }

    junitPlatformTest {
        jvmArgs '-ea'
    }

    jar {
        baseName = 'tarro-' + project.name.replace('.', '-')
        manifest {
            attributes(
                'Implementation-Title': "${project.group}:${project.name}",
                'Implementation-Version': project.version,
                'Created-By': javaVersion(),
                'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
                'Built-By': System.getProperty('user.name'),
                'Build-Commit': getGitHash(),
                // FIXME: This Last-Modified-Time stamp causes the JAR to rebuild
                //        every time. Find a way to disable it except for release
                //        builds.
                // 'Last-Modified-Time': mostRecentFileModifyTime()
            )
        }
        from {
            "${project.rootDir}/LICENSE"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    task unitTest {
        // Run the tests, excluding tests tagged as slow. This '-T <tag>'
        // command line argument replicates what the 'junitPlatform { }' closure
        // does when you say 'filters { tags { excludes ... } }'. Since at the
        // moment (v1.0.2 of the Gradle plugin) Jupiter doesn't provide a way to
        // conditionally set the excludes based on the task you're trying to do,
        // this is the workaround. So use `$ gradle unitTest` to run quick tests
        // and `$ gradle test` to include the slow-running ones.
        doLast {
            junitPlatformTest {
                jvmArgs '-ea'
                args '-T slow'
            }
        }
    }
    unitTest.dependsOn testClasses
    unitTest.finalizedBy junitPlatformTest
}


import java.nio.file.Files
import java.nio.file.Paths
import java.time.Instant
